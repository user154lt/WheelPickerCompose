
plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-opt-in=dev.chrisbanes.snapper.ExperimentalSnapperApi",
                "-Xjvm-default=all-compatibility"
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    namespace 'com.commandiron.wheel_picker_compose'
}

dependencies {
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.material3:material3:1.2.1"

    implementation "dev.chrisbanes.snapper:snapper:0.3.0"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.user154lt'
                artifactId = 'wheel-picker-compose'
                version = '1.1.12'
            }
        }
    }
}